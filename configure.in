AC_INIT([Loggerithim], [7.0.1], [gphat@loggerithim.org])
AC_CONFIG_SRCDIR([lib/Loggerithim/Handler.pm])

m4_include([inst/aclocal.m4])

AC_PROG_INSTALL
AC_PROG_APACHE
AC_SUBST([INSTALL])

AC_ARG_WITH(perl,
	[AC_HELP_STRING([--with-perl=PATH], [path to perl])],
	[if ! test -x "$with_perl"; then
		AC_MSG_ERROR([$with_perl cannot be executed])
	 fi
	 PERL="$with_perl"]
)
if test -z "$PERL"; then
	AC_CHECK_PROGS(PERL, [perl])
fi

AC_ARG_WITH(openssl,
	[AC_HELP_STRING([--with-openssl=PATH], [path to openssl])],
	[if ! test -x "$with_openssl"; then
		AC_MSG_ERROR([$with_openssl cannot be executed])
	 fi
	 OPENSSL="$with_openssl"]
)
if test -z "$OPENSSL"; then
	AC_CHECK_PROGS(OPENSSL, [openssl])
fi

if test -z "$OPENSSL"; then
	AC_MSG_ERROR([Could not find OpenSSL binary, openssl!])
fi

AC_ARG_WITH(cache-dir,
	[AC_HELP_STRING([--with-cache-dir=PATH], [path for cache directory])],
	[CACHEDIR="$with_cache_dir/loggerithim" ],
	[CACHEDIR="/var/cache/loggerithim" ]
)

AC_PROG_PERL_MODULES([Apache Apache::Cookie Apache::DBI Apache::Session Apache::Singleton Class::DBI DateTime DateTime::Event::Cron DateTime::Format::Pg DBD::Pg DBI Digest::MD5 GD GD::Polyline HTML::Template Math::NumberCruncher Net::SMTP Net::SSLeay Proc::Daemon Time::HiRes URI XML::XPath], ,
	AC_MSG_ERROR([Required module missing!])
)

AC_PROG_PERL_MODULES([IPC::SharedCache],
	HTMLCACHING=1,
	AC_MSG_WARN([Will not enable HTML::Template caching.]); HTMLCACHING=0
)
AC_PROG_PERL_MODULES([Cache::FileCache],
	CACHING=1,
	AC_MSG_WARN([Will not enable caching.]); CACHING=0
)

AC_CHECK_PROGS(POD2MAN, [pod2man])

if test -z "$HTTP_USER"; then
	AC_MSG_WARN([Could not determine Apache User, defaulting to nobody.])
	HTTP_USER="nobody"
fi

if test -z "$HTTP_GROUP"; then
	AC_MSG_WARN([Could not determine Apache Group, defaulting to nobody.])
	HTTP_GROUP="nobody"
fi

AC_SUBST([HTMLCACHING])
AC_SUBST([CACHING])
AC_SUBST([PERL])
AC_SUBST([OPENSSL])
AC_SUBST([POD2MAN])
AC_SUBST([HTTP_ROOT])
AC_SUBST([CACHEDIR])

AC_CONFIG_FILES([Makefile
				 cron/Dispatcher.pl
				 cron/EventHandler.pl
				 cron/Smeeper.pl
				 inst/startup.pl
				 inst/lr-httpd.conf
				 inst/loggerserver.init])

AC_OUTPUT
